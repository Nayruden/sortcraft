plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(':common').loom.accessWidenerPath
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // For shadowing SnakeYAML
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    // Architectury API for Fabric
    modImplementation "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle(project(path: ':common', configuration: 'transformProductionFabric'))

    // Shadow SnakeYAML into the mod jar
    shadowBundle "org.yaml:snakeyaml:${rootProject.snakeyaml_version}"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand(
            'mod_id': rootProject.mod_id,
            'mod_name': rootProject.mod_name,
            'mod_version': project.version, // Use full version with MC version suffix
            'mod_author': rootProject.mod_author,
            'mod_description': rootProject.mod_description,
            'minecraft_version': rootProject.minecraft_version,
            'supported_minecraft_versions': rootProject.fabric_minecraft_version,
            'fabric_loader_version': rootProject.fabric_loader_version,
            'architectury_version': rootProject.architectury_version
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
    exclude 'architectury.common.json'
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

