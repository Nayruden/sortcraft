plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(':common').loom.accessWidenerPath
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // For shadowing SnakeYAML
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    // Architectury API for NeoForge
    modImplementation "dev.architectury:architectury-neoforge:${rootProject.architectury_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle(project(path: ':common', configuration: 'transformProductionNeoForge'))

    // Shadow SnakeYAML into the mod jar
    shadowBundle "org.yaml:snakeyaml:${rootProject.snakeyaml_version}"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand(
            'mod_id': rootProject.mod_id,
            'mod_name': rootProject.mod_name,
            'mod_version': project.version, // Use full version with MC version suffix
            'mod_author': rootProject.mod_author,
            'mod_description': rootProject.mod_description,
            'minecraft_version': rootProject.minecraft_version,
            'supported_minecraft_versions': rootProject.neoforge_minecraft_version,
            'neoforge_version': rootProject.neoforge_version,
            'architectury_version': rootProject.architectury_version
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
    exclude 'architectury.common.json'
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

